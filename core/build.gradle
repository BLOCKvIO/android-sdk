apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion 28

  defaultConfig {
    minSdkVersion 19
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
  implementation 'com.neovisionaries:nv-websocket-client:2.8'
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
  implementation 'io.reactivex.rxjava2:rxjava:2.2.12'
  implementation 'androidx.appcompat:appcompat:1.1.0'
  implementation 'io.blockv.faces-alpha:image-face:4.0.0-alpha.4'
  implementation 'io.blockv.faces-alpha:image-progress-face:4.0.0-alpha.4'
  implementation 'io.blockv.faces-alpha:image-policy-face:4.0.0-alpha.7'
  implementation 'io.blockv.faces-alpha:image-layered-face:4.0.0-alpha.5'
  implementation 'io.blockv.faces-alpha:web-face:4.0.0-alpha.8'
  implementation 'androidx.room:room-runtime:2.1.0'
  kapt 'androidx.room:room-compiler:2.1.0'
  implementation 'androidx.room:room-rxjava2:2.1.0'
  implementation 'androidx.paging:paging-runtime:2.1.0'
  implementation 'androidx.paging:paging-rxjava2:2.1.0'

  implementation project(':common')
  implementation project(':face')
  implementation project(':faceview')

  androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  testImplementation 'junit:junit:4.12'
}
repositories {
  mavenCentral()
}

def libraryGroupId = 'io.blockv.sdk-alpha'
def libraryArtifactId = 'core'
def libraryVersion = '4.0.0-alpha.54'

task sourceJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier "sources"
}

publishing {
  publications {
    core(MavenPublication) {
      groupId libraryGroupId
      artifactId libraryArtifactId
      version libraryVersion
      artifact(sourceJar)
      artifact("$buildDir/outputs/aar/${artifactId}-release.aar")

      pom.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')
        def common = dependenciesNode.appendNode('dependency')
        common.appendNode('groupId', 'io.blockv.sdk-alpha')
        common.appendNode('artifactId', 'common')
        common.appendNode('version', '3.0.0-alpha.23')
        def paging = dependenciesNode.appendNode('dependency')
        paging.appendNode('groupId', 'androidx.paging')
        paging.appendNode('artifactId', 'paging-runtime')
        paging.appendNode('version', '[2.1.0,2.9.999)')
        def pagingRx = dependenciesNode.appendNode('dependency')
        pagingRx.appendNode('groupId', 'androidx.paging')
        pagingRx.appendNode('artifactId', 'paging-rxjava2')
        pagingRx.appendNode('version', '[2.1.0,2.9.999)')
        def versionPlugin = asNode()
          .appendNode('build')
          .appendNode('plugins')
          .appendNode('plugin')
        versionPlugin.appendNode('groupId', 'org.codehaus.mojo')
        versionPlugin.appendNode('artifactId', 'versions-maven-plugin')
        versionPlugin.appendNode('version', '2.1')
        def configuration = versionPlugin.appendNode('configuration')
        configuration.appendNode('rulesUri', 'https://raw.githubusercontent.com/BLOCKvIO/android-sdk/master/maven-rules.xml')

      }

    }
  }
  repositories {
    maven {
      url "https://maven.blockv.io/artifactory/Blockv-Debug/"
      credentials {
        username = "${blockv_artifactory_username}"
        password = "${blockv_artifactory_password}"
      }
    }
  }
}

