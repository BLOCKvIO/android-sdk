apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven-publish'

android {
  compileSdkVersion 27
  buildToolsVersion "27.0.3"

  defaultConfig {
    minSdkVersion 19
    targetSdkVersion 27
    versionCode 1
    versionName "1.0"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation 'com.neovisionaries:nv-websocket-client:2.5'
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
  implementation 'io.reactivex.rxjava2:rxjava:2.2.8'
  implementation 'com.android.support:appcompat-v7:27.1.1'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
  implementation("io.blockv.faces:image-face:3.0.0")
    {
      exclude group: 'io.blockv.sdk'
    }
  implementation("io.blockv.faces:image-progress-face:3.0.0")
    {
      exclude group: 'io.blockv.sdk'
    }
  implementation("io.blockv.faces:image-policy-face:2.0.0")
    {
      exclude group: 'io.blockv.sdk'
    }
  implementation("io.blockv.faces:image-layered-face:2.0.0")
    {
      exclude group: 'io.blockv.sdk'
    }
  implementation("io.blockv.faces:web-face:2.0.0")
    {
      exclude group: 'io.blockv.sdk'
    }
  implementation project(':common')
  implementation project(':face')
  implementation project(':faceview')

  androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  testImplementation 'junit:junit:4.12'
}
repositories {
  mavenCentral()
}

def libraryGroupId = 'io.blockv.sdk'
def libraryArtifactId = 'core'
def libraryVersion = '3.1.0'

task sourceJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier "sources"
}

publishing {
  publications {
    core(MavenPublication) {
      groupId libraryGroupId
      artifactId libraryArtifactId
      version libraryVersion
      artifact(sourceJar)
      artifact("$buildDir/outputs/aar/${artifactId}-release.aar")

      pom.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')
        def common = dependenciesNode.appendNode('dependency')
        common.appendNode('groupId', 'io.blockv.sdk')
        common.appendNode('artifactId', 'common')
        common.appendNode('version', '[2.0.0,3.0.0)')
        def versionPlugin = asNode()
          .appendNode('build')
          .appendNode('plugins')
          .appendNode('plugin')
        versionPlugin.appendNode('groupId', 'org.codehaus.mojo')
        versionPlugin.appendNode('artifactId', 'versions-maven-plugin')
        versionPlugin.appendNode('version', '2.1')
        def configuration = versionPlugin.appendNode('configuration')
        configuration.appendNode('rulesUri', 'https://raw.githubusercontent.com/BLOCKvIO/android-sdk/master/maven-rules.xml')

      }

    }
  }
  repositories {
    maven {
      url "https://maven.blockv.io/artifactory/BLOCKv/"
      credentials {
        username = "${blockv_artifactory_username}"
        password = "${blockv_artifactory_password}"
      }
    }
  }
}

